plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'net.minecrell.licenser' version '0.3'
}

group = 'org.natrolite'
description =
        'Vault is a Permissions and Economy API to allow plugins to more easily hook ' +
                'into these systems without needing to hook each individual system themselves.'

ext {
    homepage = 'https://natrolite.org/'

    licenseName = 'GNU Lesser General Public License (LGPL)'
    licenseUrl = 'https://www.gnu.org/licenses/lgpl.txt'
    licenseDist = 'repo'

    issueSystem = 'GitHub Issues'
    issueUrl = 'https://github.com/natrolite/vault-api/issues'

    ciSystem = 'Travis CI'
    ciUrl = 'https://travis-ci.org/natrolite/vault-api'

    scmUrl = 'https://github.com/natrolite/vault-api'
    scmConnection = 'scm:git:git://github.com/natrolite/vault-api.git'
    scmDevConnection = 'scm:git:ssh://github.com:natrolite/vault-api.git'
}

ext {
    javaVersion = '1.7'
    encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.natrolite.org/repository/maven-releases/' }
}

dependencies {
    compileOnly group: 'org.bukkit', name: 'bukkit', version: '1.9-R0.1-SNAPSHOT'
    testCompile group: 'org.bukkit', name: 'bukkit', version: '1.9-R0.1-SNAPSHOT'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

license {
    header = file('HEADER')
    include '**/*.java'
    newLine = true
}

processResources {
    from 'LICENSE'
}

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = encoding
}

compileTestJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = encoding
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

apply from: 'gradle/deploy.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}